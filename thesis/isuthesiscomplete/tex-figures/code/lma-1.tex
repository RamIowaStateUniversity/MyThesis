\begin{figure}[ht!]
% \centering
\begin{lstlisting}[numbers=left, tabsize=4, escapechar=@, caption={Local may alias},label={lst:lma-code}, lastline = 59] 
m: output collection[string][int] of string;
p: Project = input;
type T= {gen : string, kill : string};
type T1= {in : set of set of string, out : set of set of string};
cfg : CFG;

initPhase := traversal(node: CFGNode) : T {
	cur_val : T = {"", ""};
	if(node.defVariables!="")
		cur_val.kill = node.defVariables;
	if(def(node.expr)) {
		if(node.expr.kind == ExpressionKind.VARDECL || node.expr.kind == ExpressionKind.ASSIGN) {
			if(def(node.rhs)) {
				if(node.rhs.kind == ExpressionKind.VARACCESS) {
					cur_val.gen = node.rhs.variable;
				}
			}
		}
	}
	return cur_val;
};

analysisPhase := traversal(node: CFGNode) : T1 {
	inset : set of set of string;
	outset : set of set of string;
	cur_val : T1 = {inset, outset};
	if(def(getValue(node))) {
		cur_val = getValue(node);
	}	
	preds := node.predecessors;
	foreach(pred_node:CFGNode=preds) {	
		pred := getValue(pred_node);
		if(def(pred))
			cur_val.in = union1(cur_val.in, pred.out);
	}
	cur_val.out = setClone(cur_val.in);
	genkill := getValue(node, initPhase);
	if(genkill.kill != "") {
		foreach(tmp:set of string=cur_val.in) {
			if(contains(tmp, genkill.kill)) {
				tmp2:=setClone(tmp);
				remove(cur_val.out, tmp2);
				remove(tmp2, genkill.kill);
				if(len(tmp2)!=0) {
					add(cur_val.out, tmp2);
				}
			}
		}
		if(genkill.gen != "") {
			flag := false;
			tmpSet := setClone(cur_val.out);
			foreach(tmp:set of string=tmpSet) {
				if(contains(tmp, genkill.gen)) {
					tmp3:=setClone(tmp);
					flag = true;
					remove(cur_val.out, tmp3);
					add(tmp3, genkill.kill);
					add(cur_val.out, tmp3);
				}
			}
			if(flag == false) {
				tmp1 : set of string;
				add(tmp1, genkill.gen);add(tmp1, genkill.kill);
				add(cur_val.out, setClone(tmp1));
			}
		}
		else {
				tmp4 : set of string;
				add(tmp4, genkill.kill);
				add(cur_val.out, setClone(tmp4));
		}
	}
	return cur_val;
};

fixp1 := fixp(curr, prev: T1) : bool {
	if (difference1(curr.out, prev.out) == 0)
		return true;	
	return false;
};

q_all := visitor {
	before node: Method -> {
		clear(initPhase);clear(analysisPhase);

		cfg = getcfg(node);
		traverse(cfg, TraversalDirection.FORWARD, TraversalKind.HYBRID, initPhase);
		traverse(cfg, TraversalDirection.FORWARD, TraversalKind.HYBRID, analysisPhase, fixp1);
	}
};

visit(p, q_all);
\end{lstlisting}
\end{figure}