\begin{figure}[ht!]
% \centering
\begin{lstlisting}[numbers=left, tabsize=4, escapechar=@, caption={Available expression analysis},label={lst:ae-code},firstline=59, firstnumber=59] 
m: output collection[string][int] of string;
p: Project = input;

type T1 = {in: set of set of string, out: set of set of string};
allExprset: set of set of string;
cfg: CFG;

str: set of string;
str1: set of set of string;

node_expr := visitor {
	before node: Expression -> {
		switch (node.kind) {
			case ExpressionKind.LT,ExpressionKind.OP_ADD, ExpressionKind.OP_SUB, ExpressionKind.OP_INC, ExpressionKind.OP_MULT, ExpressionKind.OP_DIV, ExpressionKind.OP_MOD, ExpressionKind.OP_DEC, ExpressionKind.GT, ExpressionKind.EQ, ExpressionKind.NEQ, ExpressionKind.LTEQ, ExpressionKind.GTEQ, ExpressionKind.LOGICAL_NOT, ExpressionKind.LOGICAL_AND, ExpressionKind.LOGICAL_OR, ExpressionKind.BIT_AND, ExpressionKind.BIT_OR, ExpressionKind.BIT_NOT, ExpressionKind.BIT_XOR, ExpressionKind.BIT_LSHIFT, ExpressionKind.BIT_RSHIFT, ExpressionKind.BIT_UNSIGNEDRSHIFT:
				add(str, string(node.kind)); 
				foreach(j:int;def(node.expressions[j])) {
					visit(node.expressions[j]);
				}
				break;
			case ExpressionKind.ASSIGN:
				foreach(j:int;def(node.expressions[j])) {
					if(j!=0) {
						visit(node.expressions[j]);
					}
				}
				break;
			case ExpressionKind.VARACCESS:
				add(str, node.variable);
				break;
			case ExpressionKind.VARDECL:
				visit(node.variable_decls[0].initializer);
				break;
			case ExpressionKind.LITERAL:
				add(str, node.literal);
				break;
			case ExpressionKind.METHODCALL:
				clear(str);
				stop;
			default:break;
		}
		stop;
	}
};

allExprTraversal := traversal(node: CFGNode): set of string {
	init_set : set of string;
	str = init_set;
	if(def(node.expr)) {
		visit(node.expr, node_expr);
		if(len(str)!=0) {
			add(allExprset, setClone(str));
		}
	}
	return setClone(str);
};

killTraversal := traversal(node: CFGNode): set of set of string {
	init_set1 : set of set of string;
	str1 = init_set1;
	if(def(node.defVariables)) {
		foreach(aa:set of string=allExprset) {
				if(contains(aa, node.defVariables)) {
					add(str1, aa);
				}
		}
	}
	return setClone(str1);
};

avail_expr := traversal(node: CFGNode): T1 {
	in_set : set of set of string;
	out_set : set of set of string;
	cur_value : T1;
	if(node.id==0) {
		cur_value = {in_set, out_set};
	}
	else
		cur_value = {setClone(allExprset), out_set};
	if(def(getValue(node))) {
		cur_val1 := getValue(node);
		cur_value = clone(cur_val1);
	}
	preds := node.predecessors;
	foreach(pred_node:CFGNode=preds) {	
		pred := getValue(pred_node);
		if(def(pred))
			cur_value.in = intersection1(cur_value.in, pred.out);
	}
	genkill := getValue(node, allExprTraversal);
	killset := getValue(node, killTraversal);
	cur_value.out = setClone(cur_value.in);
	removeAll(cur_value.out, killset);
	if(len(genkill)!=0)
		add(cur_value.out, genkill);
	return cur_value;
};


fixp1 := fixp(curr, prev: T1) : bool {
	if (difference1(curr.out, prev.out) == 0)
		return true;	
	return false;
};

def := visitor {
	before node: Method -> {
		clear(allExprTraversal);clear(avail_expr);clear(killTraversal);clear(allExprset);
		cfg = getcfg(node);
		traverse(cfg, TraversalDirection.FORWARD, TraversalKind.HYBRID, allExprTraversal);
		traverse(cfg, TraversalDirection.FORWARD, TraversalKind.HYBRID, killTraversal);
		traverse(cfg, TraversalDirection.FORWARD, TraversalKind.HYBRID, avail_expr, fixp1);		
	}
};

visit(p, def);
\end{lstlisting}
\end{figure}